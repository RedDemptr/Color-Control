/*Active whenever a game is running, independent of game mode.
Keeps cubes intact and handles capturing, as well as a number of
various 
*/

// This chain controls the physical environment/level and keeps it intact.
# HandleGame
>{"type":"repeating-chain","conditional":false,"auto":false}
// Cube stuff
	// Prevents players from breaking black concrete.
 /execute @e[type=area_effect_cloud,tag=cube,score_entropyCycle_min=1] ~ ~ ~ function loops:border
	// Controls block capturing.
 /execute @a[team=Blue,score_brokeConcrete=1,score_brokeConcrete_min=1] ~ ~ ~
 	execute @e[type=area_effect_cloud,tag=cube,c=1] ~ ~ ~
 		fill ~-2 ~-2 ~-2 ~2 ~2 ~2 minecraft:concrete color=blue replace minecraft:air
 /execute @a[team=Red,score_brokeConcrete=1,score_brokeConcrete_min=1] ~ ~ ~
 	execute @e[type=area_effect_cloud,tag=cube,c=1] ~ ~ ~
 		fill ~-2 ~-2 ~-2 ~2 ~2 ~2 minecraft:concrete color=red replace minecraft:air
 /scoreboard players set @a[score_brokeConcrete_min=1] brokeConcrete 0
 /execute @e[type=area_effect_cloud,tag=cube,score_entropyCycle_min=1] ~ ~ ~
 		fill ~-1 ~-1 ~-1 ~1 ~1 ~1 minecraft:concrete color=white replace minecraft:air
 /execute @e[type=area_effect_cloud,tag=cube,score_entropyCycle_min=1] ~ ~ ~
 		fill ~-2 ~-2 ~-2 ~2 ~2 ~2 minecraft:concrete color=white replace minecraft:air
 	// Shows particles on captured cubes.
 /execute @e[tag=cube,score_blocksRed_min=9,score_blocksRed=9] ~ ~ ~
 	particle blockdust ~ ~3 ~ 0.7 0 0.7 0 1 force @a 57595
 /execute @e[tag=cube,score_blocksBlue_min=9,score_blocksBlue=9] ~ ~ ~
 	particle blockdust ~ ~3 ~ 0.7 0 0.7 0 1 force @a 45307

// Arena preservation
	// Prevents players from placing blocks on spawn locations.
 /fill 1030 51 1030 1030 52 1030 minecraft:air 0 destroy
 /fill 970 51 970 970 52 970 minecraft:air 0 destroy
	// Constantly resets the spawn cubes to prevent them from being damaged.
 /setblock 970 47 970 minecraft:structure_block mode=load replace {metadata:"",mirror:"NONE",ignoreEntities:1b,powered:0b,seed:0L,author:"Sybillian",rotation:"NONE",posX:-2,mode:"LOAD",posY:-1,sizeX:20,posZ:-2,integrity:1.0f,showair:0b,name:"spawn_red",id:"minecraft:structure_block",sizeY:5,sizeZ:20,showboundingbox:0b}
 /setblock 1030 47 1030 minecraft:structure_block mode=load replace {metadata:"",mirror:"NONE",ignoreEntities:1b,powered:0b,seed:0L,author:"Sybillian",rotation:"NONE",posX:-17,mode:"LOAD",posY:-1,sizeX:27,posZ:-17,integrity:1.0f,showair:0b,name:"spawn_blue",id:"minecraft:structure_block",sizeY:8,sizeZ:27,showboundingbox:0b}
 /setblock 970 48 970 minecraft:redstone_block
 /setblock 1030 48 1030 minecraft:redstone_block
 /setblock 1030 47 1030 minecraft:concrete color=gray
 /setblock 970 47 970 minecraft:concrete color=gray
	// Rotates armor stands.
 /tp @e[type=armor_stand,tag=display] ~ ~ ~ ~0.2 ~

// Player handling
	// Handles players joining while a game is going on.
 /scoreboard players tag @a[score_joinGame_min=1] add tpGame
 /teleport @a[tag=tpGame] 1000 50 1000
 /gamemode 3 @a[tag=tpGame]
 /scoreboard teams join Spectators @a[tag=tpGame]
 /scoreboard players tag @a[tag=tpGame] remove tpGame
	// Gives players a wooden pickaxe upon spawning, as well as iron boots which are removed after 5 seconds.
 /give @a[team=!Spectators,score_respawnTrack=1,score_respawnTrack_min=1] minecraft:wooden_pickaxe 1 0 {Unbreakable:1b,AttributeModifiers:[{AttributeName:"generic.attackDamage",Name:"generic.attackDamage",Amount:4,Operation:0,UUIDLeast:211354,UUIDMost:771025,Slot:"mainhand"},{AttributeName:"generic.attackSpeed",Name:"generic.attackSpeed",Amount:-0.5,Operation:1,UUIDLeast:445289,UUIDMost:45367,Slot:"mainhand"}],ench:[{id:71,lvl:1}]} // Give respawning players thier items
 /replaceitem entity @a[team=!Spectators,score_respawnTrack=1,score_respawnTrack_min=1] slot.armor.feet minecraft:iron_boots 1 0 {HideFlags:63,Unbreakable:1b,AttributeModifiers:[{AttributeName:"generic.knockbackResistance",Name:"generic.knockbackResistance",Amount:100,Operation:1,UUIDLeast:548037,UUIDMost:36657,Slot:"feet"}],display:{Name:"Spawn Boots",Lore:["Protects from knockback and damage.","Removed after 5 seconds."]},ench:[{id:0,lvl:100},{id:71,lvl:1}]}
 /clear @a[score_respawnTrack_min=100] minecraft:iron_boots 0 1 {Unbreakable:1b}
 	// Tests for a full team having left.
 /scoreboard players test RedPlayers 0 0
>{"conditional":true}
 /execute @e[type=area_effect_cloud,tag=BlueWinForfeit] ~ ~ ~ blockdata ~ ~ ~ {auto:1b}
>{"conditional":false}
 /scoreboard players test BluePlayers 0 0
>{"conditional":true}
 /execute @e[type=area_effect_cloud,tag=RedWinForfeit] ~ ~ ~ blockdata ~ ~ ~ {auto:1b}
>{"conditional":false}

// Advancements
 /execute @a[m=0] ~ ~ ~ scoreboard players tag @p[y=20,dy=10,m=0] add testCD
 /execute @a[m=0] ~ ~ ~ advancement grant @p[y=49,dy=10,tag=testCD,m=0] only cc:cheat_death
 /scoreboard players tag @a remove testCD {Health:0f}
 /execute @a[m=0] ~ ~ ~ scoreboard players tag @p[y=-10,dy=10,m=0] add advDia {Inventory:[{id:"minecraft:diamond_helmet"},{id:"minecraft:diamond_chestplate"},{id:"minecraft:diamond_leggings"},{id:"minecraft:diamond_boots"}]}
 /execute @a[m=0] ~ ~ ~ advancement grant @p[tag=advDia,m=0] only cc:fall_dia
 
// Location-based stuff (map boundries, spawn zones...)
 /execute @a[m=0] ~ ~ ~ function loops:arena_edge