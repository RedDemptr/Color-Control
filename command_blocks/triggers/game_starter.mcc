// Ensures that the game will only start when both teams are ready and have at least one player each.
# TestStart
>{"type":"repeating-chain","conditional":false,"auto":true}
 /testfor @a[team=Red]
 !stats block set AffectedEntities RedPlayers gameSettings
 /testfor @a[team=Blue]
 !stats block set AffectedEntities BluePlayers gameSettings
 /scoreboard players test TeamsReady gameSettings 0 1
>{"conditional":true}
 /scoreboard players set StartGame gameSettings 0
>{"conditional":false}
 /scoreboard players test TeamsReady gameSettings 2 2
>{"conditional":true}
 /scoreboard players test RedPlayers gameSettings 1 *
 /scoreboard players test BluePlayers gameSettings 1 *
 /scoreboard players add StartGame gameSettings 1
>{"conditional":false}
 /scoreboard players test TeamsReady gameSettings 2 2
>{"conditional":true}
 /scoreboard players test RedPlayers gameSettings 0 0
 /scoreboard players test BluePlayers gameSettings 1 *
 /title @a actionbar {"text":"Red team needs at least one player to start.","color":"dark_red"}
>{"conditional":false}
 /scoreboard players test TeamsReady gameSettings 2 2
>{"conditional":true}
 /scoreboard players test BluePlayers gameSettings 0 0
 /scoreboard players test RedPlayers gameSettings 1 *
 /title @a actionbar {"text":"Blue team needs at least one player to start.","color":"dark_red"}
>{"conditional":false}
 /scoreboard players test TeamsReady gameSettings 2 2
>{"conditional":true}
 /scoreboard players test RedPlayers gameSettings 0 0
 /scoreboard players test BluePlayers gameSettings 0 0
 /title @a actionbar {"text":"Both teams need at least one player to start.","color":"dark_red"}

// Counts down to the beginning of the game.
# TriggerStart
>{"type":"repeating-chain","conditional":false,"auto":true}
 /scoreboard players test StartGame gameSettings 20 20
>{"conditional":true}
 /title @a subtitle {"text":"Game beginning in...","color":"blue"}
 /title @a title {"text":"3","color":"red"}
 /execute @a ~ ~ ~ playsound minecraft:block.dispenser.dispense master @p ~ ~ ~ 1 1
>{"conditional":false}
 /scoreboard players test StartGame gameSettings 40 40
>{"conditional":true}
 /title @a subtitle {"text":"Game beginning in...","color":"blue"}
 /title @a title {"text":"2","color":"yellow"}
 /execute @a ~ ~ ~ playsound minecraft:block.dispenser.dispense master @p ~ ~ ~ 1 1
>{"conditional":false}
 /scoreboard players test StartGame gameSettings 60 60
>{"conditional":true}
 /title @a subtitle {"text":"Game beginning in...","color":"blue"}
 /title @a title {"text":"1","color":"green"}
 /execute @a ~ ~ ~ playsound minecraft:block.dispenser.dispense master @p ~ ~ ~ 1 1
>{"conditional":false}
 /scoreboard players test StartGame gameSettings 80 80
>{"conditional":true}
 /title @a subtitle {"text":"Good luck!","color":"blue"}
 /title @a title {"text":"Begin!","color":"green"}
 /execute @a ~ ~ ~ playsound minecraft:entity.experience_orb.pickup master @p ~ ~ ~ 1 1

// Runs a few operations that have to be done before teleporting the players.
 /scoreboard players set TeamsReady gameSettings 0
 /fill -996 32 -1008 -992 33 -992 minecraft:air 0 replace minecraft:barrier
 /fill -996 32 -1008 -992 33 -992 minecraft:air 0 replace minecraft:stained_glass
 /teleport @a[team=!] 0 51 0
 /scoreboard players set StartGame gameSettings 0
 /execute @e[type=area_effect_cloud,name=StartGame] ~1 ~ ~ blockdata ~ ~ ~ {auto:1b}

// Performs one-time operations to prepare for and kickstart the game.
# StartGame
>{"type":"impulse-chain","conditional":false,"auto":false}
 /scoreboard players set TickDivide gameSettings 20
 /scoreboard players operation RotationSeconds gameSettings = TimerSetting rotateCubes
 /scoreboard players operation RotationSeconds gameSettings /= TickDivide gameSettings
 /scoreboard players set MinuteDivide gameSettings 1200
 /scoreboard players operation TimeSeconds gameSettings = GameLength gameSettings
 /scoreboard players operation TimeSeconds gameSettings /= MinuteDivide gameSettings
 /scoreboard players set GameActive gameSettings 1
 /scoreboard teams join Spectators @a[team=]
 /tp @a[team=Spectators] 1000 60 1000
 /tp @a[team=Red] 970 51 970
 /tp @a[team=Blue] 1030 51 1030
 /spawnpoint @a[team=Red] 970 51 970
 /spawnpoint @a[team=Blue] 1030 51 1030
 /spawnpoint @a[team=Spectators] 1000 60 1000
 /gamemode 0 @a
 /gamemode 3 @a[team=Spectators]
 /give @a[team=!Spectators] minecraft:wooden_pickaxe 1 0 {Unbreakable:1b,AttributeModifiers:[{AttributeName:"generic.attackDamage",Name:"generic.attackDamage",Amount:4,Operation:0,UUIDLeast:211354,UUIDMost:771025,Slot:"mainhand"},{AttributeName:"generic.attackSpeed",Name:"generic.attackSpeed",Amount:-0.5,Operation:1,UUIDLeast:445289,UUIDMost:45367,Slot:"mainhand"}],ench:[{id:71,lvl:1}]}
 /scoreboard players set Red Lives 15
 /scoreboard players set Blue Lives 15
 /scoreboard players set Red Points 0
 /scoreboard players set Blue Points 0
 /scoreboard players operation Timer rotateCubes = TimerSetting rotateCubes
 /scoreboard players set @e[type=area_effect_cloud,tag=cube] pointTimerBlue 0
 /scoreboard players set @e[type=area_effect_cloud,tag=cube] pointTimerRed 0
 /scoreboard players set @e[type=area_effect_cloud,tag=cube] spawnTimer 0
 /scoreboard players set @e[type=area_effect_cloud,tag=cube] blocksBlue 0
 /scoreboard players set @e[type=area_effect_cloud,tag=cube] blocksRed 0
 /scoreboard teams join R_Display Red
 /scoreboard teams join B_Display Blue
 /execute @e[type=area_effect_cloud,name=HandleGame] ~1 ~ ~ blockdata ~ ~ ~ {auto:1b}
 /execute @e[type=area_effect_cloud,name=TriggerCubes] ~1 ~ ~ blockdata ~ ~ ~ {auto:1b}
 /execute @e[type=area_effect_cloud,name=PrepGame] ~1 ~ ~ blockdata ~ ~ ~ {auto:1b}
 /execute @e[type=area_effect_cloud,name=StartGame] ~1 ~ ~ blockdata ~ ~ ~ {auto:0b}